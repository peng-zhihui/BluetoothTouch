import java.util.regex.Pattern

project.ext {
    versionCode = '082'
    versionName = '0.8.2'
    archivesBaseName = project.name + '-' + project.versionName
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
    }
}

task update { // Task to update version in accessory filter
    def accessoryFile = file('res/xml/accessory_filter.xml')
    def accessoryText = accessoryFile.getText()

    println 'Updating accessory filter version to: ' + project.versionName
    def version = Pattern.compile('\"Balanduino\" version=\"(.*?)\"').matcher(accessoryText)
    accessoryText = version.replaceAll('\"Balanduino\" version=\"' + project.versionName + '\"')

    accessoryFile.write(accessoryText)
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'


    defaultConfig {
        targetSdkVersion 29
        versionName = project.versionName
        applicationId "xyz.pengzhihui.BTremote"
        flavorDimensions "default"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        basic {
            minSdkVersion 28
            versionCode = Integer.parseInt('1000' + project.versionCode)
        }
        usb {
            minSdkVersion 28
            versionCode = Integer.parseInt('1200' + project.versionCode)
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
        basic {
            java.srcDirs = ['flavors/basic/src']
            res.srcDirs = ['flavors/basic/res']
        }
        usb {
            manifest.srcFile 'flavors/usb/AndroidManifest.xml'
            java.srcDirs = ['flavors/usb/src']
            res.srcDirs = ['flavors/usb/res']
        }
    }
}

repositories {
    jcenter()
    maven { url 'libs' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation 'com.jjoe64:graphview:3.1.4'
    implementation 'com.viewpagerindicator:viewpagerindicator:2.4.1'
    implementation 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    usbImplementation 'com.physicaloid:physicaloid:1.0' // Only supported on API level above 12
}

// These values are all set in my global gradle.properties file
if (project.hasProperty('storeFile') && project.hasProperty('storePassword') && project.hasProperty('keyAlias') && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

// Releasing:
// 1. Update versionName and versionCode
// 2. Run './gradlew clean build'
// 3. Move APK to releases directory
// 4. Upload to Github and create tag
//    * Upload APK to release tab on Github as well
// 5. Upload to Google Play Store
//    * Also take new screenshots if necessary
// 6. Update ADK version in Balanduino firmware